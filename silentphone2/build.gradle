apply plugin: 'com.android.application'
apply plugin: 'com.google.gms.google-services'

android {

    compileSdkVersion rootProject.ext.compileSdkVersion
    buildToolsVersion rootProject.ext.buildToolsVersion

    dexOptions {
        jumboMode = true
        javaMaxHeapSize "3g"
    }
    defaultConfig {
        minSdkVersion 16
        targetSdkVersion rootProject.ext.targetSdkVersion

        // Version string: major.minor or major.minor.bug-fix.  When
        // releasing an X.Y version, list only two digits rather than
        // X.Y.Z.
        versionName "4.2.1"

        // The version code (vc) computes as follows: major*1000 + minor*100 + bug-fix.
        // This provides up to 100 bug-fix or small feature update releases :-)
        // To create the Android 'versionCode' the script shifts this number left by 16 and adds the
        // Jenkins build number.
        def vc = 4201

        applicationId "com.silentcircle.silentphone"

        def version = "0"
        if (project.hasProperty('build_version_numeric')) {version = build_version_numeric }
        def newVersion = (vc << 16) + Integer.parseInt(version)
        versionCode newVersion                            // the 'versionCode' goes into the AndroidManifest

        def build = "DEBUG"
        if (project.hasProperty('build_version')) {build = build_version }
        buildConfigField "String", "SPA_BUILD_NUMBER", "\"" + build + "\""

        def commit = "\"DEBUG\""
        if (project.hasProperty('build_commit')) {commit = "\"" + build_commit + "\""}
        buildConfigField "String", "SPA_BUILD_COMMIT", commit

        def date = "\"DEBUG\""
        if (project.hasProperty('build_date')) {date = "\"" + build_date + "\""}
        buildConfigField "String", "SPA_BUILD_DATE", date

        ndk {
            abiFilters "armeabi-v7a" // , "armeabi" - I don't think we see old ARM devices in the field anymore
        }
        lintOptions {
            checkReleaseBuilds false
            abortOnError false
        }
        testApplicationId "com.silentcircle.silentphone2.tests"

        // Enabling multidex support.
//        multiDexEnabled true
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }

    // Comment from Android gradle plugin documentation:
    // Note: srcDir will actually add the given folder to the existing list of source folders
    // (this is not mentioned in the Gradle documentation but this is actually the behavior).

    // We directly source-in the Java sources and don't use the jar files of the modules. The
    // modules' build script produce jar files that other apps may use.
    sourceSets {
        main {
            java {
                srcDir '../KeyManagerSupport/src/main/java'
                srcDir '../ScContactsContract/src/main/java'
                srcDir '../ImageViewTouch/src/main/java'
            }
            jniLibs.srcDirs = ['libs']
        }
    }

    productFlavors {
        normal {
            applicationId "com.silentcircle.silentphone"
        }
        dev21 {
            applicationId "com.silentcircle.silentphone"
            minSdkVersion 21
        }
    }

    signingConfigs {
        sc {
            storeFile file(".build-release/test-debug.keystore")
            storePassword "android"
            keyAlias "androiddebugkey"
            keyPassword "android"
        }
    }

    buildTypes {
        debug {
            debuggable true
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.txt'
            zipAlignEnabled true
        }
        fastDebug {
            debuggable true
            minifyEnabled false
            multiDexEnabled true
            signingConfig signingConfigs.debug
            zipAlignEnabled true
        }
        develop {
            debuggable true
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.txt'
            signingConfig signingConfigs.sc
            zipAlignEnabled true
        }
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.txt'
            signingConfig signingConfigs.sc
            zipAlignEnabled true
        }
    }
}

android.applicationVariants.all { variant ->
    variant.mergedFlavor.manifestPlaceholders = variant.buildType.debuggable ? [backup:"true"] : [backup:"false"]
}

// Example how to define a XML resource during build
//android.applicationVariants.all { variant ->
//    variant.resValue "bool", "backup", variant.buildType.isDebuggable() ? "true" : "false"
//}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    compile 'com.googlecode.libphonenumber:libphonenumber:7.2.1'
    compile 'com.googlecode.libphonenumber:geocoder:2.31'
    compile 'com.android.support:appcompat-v7:23.1.1'
    compile 'com.android.support:design:23.1.1'
    compile 'com.android.support:support-v13:23.1.1'
    compile 'com.android.support:palette-v7:23.1.1'
    compile 'com.android.support:cardview-v7:23.1.1'
    compile 'com.google.android.gms:play-services-gcm:8.3.0'
//    compile 'com.android.support:multidex:1.0.1'
    compile 'com.android.support:design:23.1.1'
    compile 'com.stripe:stripe-android:1.0.0'
}
